apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.rtCompileSdkVersion
    buildToolsVersion rootProject.ext.rtBuildToolsVersion

    defaultConfig {
        applicationId "cmu.xprize.robotutor"
        minSdkVersion rootProject.ext.rtMinSdkVersion
        targetSdkVersion rootProject.ext.rtTargetSdkVersion
        versionCode rootProject.ext.rtVersionCode
        versionName rootProject.ext.rtVersionName
    }
    buildTypes {

        // These custom buildFields are incorporated into the generated BuildConfig class
        // and may be evaluated in Java code.
        // e.g. String rtLanguage= BuildConfig.LANGUAGE_FEATURE_ID;
        //
        def BOOLEAN = "boolean"
        def STRING = "String"
        def TRUE = "true"
        def FALSE = "false"

        def LANGUAGE_FEATURE_ID = "LANGUAGE_FEATURE_ID"
        def LANGUAGE_OVERRIDE = "LANGUAGE_OVERRIDE"
        def LANGUAGE_SWAHILI = "\"LANG_SW\""
        def LANGUAGE_ENGLISH = "\"LANG_EN\""
        def LANGUAGE_UNDEFINED = "\"LANG_NULL\""

        def NO_ASR_APPS = "NO_ASR_APPS"

        release {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, FALSE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_UNDEFINED

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, FALSE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_UNDEFINED

            debuggable true
        }

        // These releases will remove the language selection toggle switch if present in the UI
        // They also override the LANG_<ID> in the enginedescriptor to force a default language
        //
        release_sw {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, TRUE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_SWAHILI
        }

        release_sw_noasr {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, TRUE
            buildConfigField BOOLEAN, NO_ASR_APPS, TRUE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_SWAHILI
        }

        release_en {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, TRUE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_ENGLISH
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:percent:24.2.0'
    compile project(':banner')
    compile project(':comp_ltkplus')
    compile project(':util')
    compile project(':mn_component')
    compile project(':listener')
    compile project(':rt_component')
    compile project(':sm_component')
    compile project(':fw_component')
    compile project(':nl_component')
    compile project(':asm_component')
    compile project(':ak_component')
    compile project(':bp_component')
    compile project(':comp_writing')
    compile project(':comp_pointtap')
    compile project(path: ':comp_ask')
    compile project(path: ':comp_session')
}
