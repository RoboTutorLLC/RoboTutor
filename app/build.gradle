apply plugin: 'com.android.application'

android {

    compileSdkVersion rootProject.ext.rtCompileSdkVersion
    buildToolsVersion rootProject.ext.rtBuildToolsVersion
    defaultConfig {
        applicationId "cmu.xprize.robotutor"
        minSdkVersion rootProject.ext.rtMinSdkVersion
        targetSdkVersion rootProject.ext.rtTargetSdkVersion
        versionCode rootProject.ext.rtVersionCode
        versionName rootProject.ext.rtVersionName
        // default is signed by the Android default key
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    // Create a variable called keystorePropertiesFile, and initialize it to your
    // keystore.properties file, in the rootProject folder.
    def keystorePropertiesFile = rootProject.file("keystore.properties")

    // Initialize a new Properties() object called keystoreProperties
    def keystoreProperties = new Properties()

    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        android {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {

        // These custom buildFields are incorporated into the generated BuildConfig class
        // and may be evaluated in Java code.
        // e.g. String rtLanguage= BuildConfig.LANGUAGE_FEATURE_ID;
        //
        def BOOLEAN = "boolean"
        def STRING  = "String"
        def TRUE    = "true"
        def FALSE   = "false"

        def LANGUAGE_FEATURE_ID = "LANGUAGE_FEATURE_ID"
        def SHOW_TUTORVERSION   = "SHOW_TUTORVERSION"
        def SHOW_DEBUGLAUNCHER  = "SHOW_DEBUGLAUNCHER"
        def LANGUAGE_OVERRIDE   = "LANGUAGE_OVERRIDE"
        def LANGUAGE_SWITCH     = "LANGUAGE_SWITCH"
        def LANGUAGE_SWAHILI    = "\"LANG_SW\""
        def LANGUAGE_ENGLISH    = "\"LANG_EN\""
        def LANGUAGE_UNDEFINED  = "\"LANG_NULL\""
        def SHOW_DEMO_VIDS      = "SHOW_DEMO_VIDS"

        def NO_ASR_APPS = "NO_ASR_APPS"

        release {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, FALSE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, FALSE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, FALSE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, FALSE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_UNDEFINED
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, TRUE

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, FALSE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, TRUE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, TRUE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, TRUE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_UNDEFINED
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, FALSE

            debuggable true
        }

        // This release includes the DEBUG selector and other various debug support features.
        //
        release_dbg {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, FALSE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, TRUE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, TRUE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, TRUE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_UNDEFINED
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, TRUE

            debuggable true
            signingConfig signingConfigs.android
        }

        release_dbg_no_demo {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, FALSE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, TRUE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, TRUE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, TRUE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_UNDEFINED
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, FALSE

            signingConfig signingConfigs.android
        }

        // These releases will remove the language selection toggle switch if present in the UI
        // They also override the LANG_<ID> in the enginedescriptor to force a default language
        //
        release_sw {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, TRUE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, TRUE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, FALSE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, FALSE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_SWAHILI
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, TRUE

            debuggable true
            signingConfig signingConfigs.android
        }

        release_sw_xprize {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, TRUE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, FALSE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, FALSE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, FALSE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_SWAHILI
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, TRUE

            debuggable true
            signingConfig signingConfigs.android
        }

        release_en {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, TRUE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, TRUE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, FALSE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, FALSE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_ENGLISH
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, TRUE

            signingConfig signingConfigs.android
        }

        release_en_xprize {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, TRUE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, FALSE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, FALSE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, FALSE
            buildConfigField BOOLEAN, NO_ASR_APPS, FALSE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_ENGLISH
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, TRUE

            signingConfig signingConfigs.android
        }

        release_sw_noasr {
            buildConfigField BOOLEAN, LANGUAGE_OVERRIDE, TRUE
            buildConfigField BOOLEAN, SHOW_TUTORVERSION, TRUE
            buildConfigField BOOLEAN, SHOW_DEBUGLAUNCHER, FALSE
            buildConfigField BOOLEAN, LANGUAGE_SWITCH, FALSE
            buildConfigField BOOLEAN, NO_ASR_APPS, TRUE
            buildConfigField STRING, LANGUAGE_FEATURE_ID, LANGUAGE_SWAHILI
            buildConfigField BOOLEAN, SHOW_DEMO_VIDS, TRUE
        }
    }

    // define apk naming behavior
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "robotutor"
            def SEP = "."
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionName

            def newApkName = project + SEP + buildType + SEP + version + ".apk"
            output.outputFile = new File(output.outputFile.parent, newApkName)
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:percent:25.2.0'
    compile project(':comp_banner')
    compile project(':comp_ltkplus')
    compile project(':util')
    compile project(':mn_component')
    compile project(':comp_listener')
    compile project(':comp_reading')
    compile project(':comp_questions')
    compile project(':sm_component')
    compile project(':fw_component')
    compile project(':nl_component')
    compile project(':comp_math')
    compile project(':comp_akira')
    compile project(':comp_bubblepop')
    compile project(':comp_writing')
    compile project(':comp_pointtap')
    compile project(path: ':comp_ask')
    compile project(path: ':comp_session')
    compile project(path: ':comp_debug')
    compile project(':comp_logging')
    compile project(path: ':comp_counting')
    compile project(path: ':comp_counting2')
    compile project(path: ':comp_numberscale')
    compile project(path: ':comp_nd')
    compile project(path: ':comp_picmatch')
}

